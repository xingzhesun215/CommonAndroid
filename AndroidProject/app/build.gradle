apply from: "../config.gradle"

if (isModele_Main.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}


android {
    compileSdkVersion versions["compileSdkVersion"]
    buildToolsVersion versions["buildToolsVersion"]

    defaultConfig {
        if (isModele_Main.toBoolean()) {
            applicationId versions["applicationId_Main"]
        }

        minSdkVersion versions["minSdkVersion"]
        targetSdkVersion versions["targetSdkVersion"]
        versionCode versions["versionCode"]
        versionName versions["versionName"]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(keyinfo["store_file"])
            keyAlias keyinfo["key_alias"]
            keyPassword keyinfo["key_password"]
            storePassword keyinfo["store_password"]
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        // apk 输出文件名配置
        variant.outputs.all { output ->
            outputFileName = rootProject.getName() + '_v' + variant.versionName + '_' + variant.buildType.name
            if (variant.buildType.name == buildTypes.release.getName()) {
                outputFileName += '_' + new Date().format('MMdd')
            }
            outputFileName += '.apk'
        }
    }

}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation project(path: ':common_library')
    if (!isModule_A.toBoolean() && isModuleA_To_Main.toBoolean()) {
        implementation project(path: ':module_a')
    }

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}