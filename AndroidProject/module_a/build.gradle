apply from: "../config.gradle"

if (isModule_A.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion versions["compileSdkVersion"]
    buildToolsVersion versions["buildToolsVersion"]

    defaultConfig {
        if (isModule_A.toBoolean()) {
            applicationId versions["applicationId_A"]
        }

        minSdkVersion versions["minSdkVersion"]
        targetSdkVersion versions["targetSdkVersion"]
        versionCode versions["versionCode"]
        versionName versions["versionName"]


        //Arouter路由使用
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            if (isModule_A.toBoolean()) {//此时为组件开发,各开发各的
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {//此时为集成开发,合 将debug目录下分开
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    exclude 'debug/**'
                }
            }
        }
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation project(path: ':common_library')

    compile 'com.alibaba:arouter-api:1.3.1'
    annotationProcessor 'com.alibaba:arouter-compiler:1.1.4'

}